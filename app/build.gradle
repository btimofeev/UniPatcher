apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId "org.emunix.unipatcher"
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34
        ndkVersion "22.1.7171670"
        versionCode 170300
        versionName "0.17.3"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-21", "-DCMAKE_BUILD_TYPE=Release",
                        "-DANDROID_ARM_MODE=arm"
                version "3.10.2"
            }
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
        }
    }

    flavorDimensions "default"
    productFlavors {
        free {
            buildConfigField "String", "RATE_URL", "\"https://github.com/btimofeev/UniPatcher\""
            buildConfigField "String", "SHARE_URL", "\"https://github.com/btimofeev/UniPatcher\""
            buildConfigField "String", "BITCOIN_ADDRESS", "\"16coztryz7xbNNDNhhf98wuHmi3hEintsW\""
        }
        google {
            buildConfigField "String", "RATE_URL", "\"market://details?id=org.emunix.unipatcher\""
            buildConfigField "String", "SHARE_URL", "\"https://play.google.com/store/apps/details?id=org.emunix.unipatcher\""
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    namespace 'org.emunix.unipatcher'
}

Properties props = new Properties()
def propFile = file('../../signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    // Kotlin
    implementation libs.kotlin.stdlib
    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.coroutines.core

    // AndroidX
    implementation libs.appcompat
    implementation libs.cardview
    implementation libs.constraintlayout
    implementation libs.core.ktx
    implementation libs.documentfile
    implementation libs.fragment.ktx
    implementation libs.lifecycle.common
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.preference.ktx
    implementation libs.material

    // Third-party
    //noinspection GradleDependency
    implementation libs.commons.io

    // DI
    implementation libs.hilt.android
    kapt libs.hilt.android.compiler

    // Logs
    implementation libs.timber

    // Crash reports
    implementation libs.acra.mail
    implementation libs.acra.notification

    // Testing
    testImplementation libs.junit
    testImplementation libs.mockito.core
}

tasks.register("deleteDependencies", Delete) {
    delete 'src/main/cpp/xdelta3/xdelta'
    delete 'src/main/cpp/xz/xz'
}

tasks.register("downloadDependencies") {
    dependsOn deleteDependencies
    def XDelta = '3.1.0'
    def xz = '5.2.4'
    def downloadDir = layout.buildDirectory.get().asFile.path

    doLast {
        download.run {
            src "https://github.com/jmacd/xdelta/archive/v${XDelta}.tar.gz"
            dest new File(downloadDir, "xdelta-${XDelta}.tar.gz")
        }
        copy {
            from tarTree(resources.gzip("${downloadDir}/xdelta-${XDelta}.tar.gz"))
            into 'src/main/cpp/xdelta3/'
        }
        file("src/main/cpp/xdelta3/xdelta-${XDelta}").renameTo(file('src/main/cpp/xdelta3/xdelta'))
        delete("src/main/cpp/xdelta3/pax_global_header")

        download.run {
            src "https://sourceforge.net/projects/lzmautils/files/xz-${xz}.tar.gz/download"
            dest new File(downloadDir, "xz-${xz}.tar.gz")
        }
        copy {
            from tarTree(resources.gzip("${downloadDir}/xz-${xz}.tar.gz"))
            into 'src/main/cpp/xz/'
        }
        file("src/main/cpp/xz/xz-${xz}").renameTo(file('src/main/cpp/xz/xz'))
    }
}
